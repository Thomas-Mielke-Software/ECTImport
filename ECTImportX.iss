; Script generated by the Inno Setup Script Wizard.    Version 2 !!!
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
AppName=CSV-Import
AppVerName=CSV-Import 6.0
DiskSpanning=no
AppPublisher=tm
AppPublisherURL=http://www.easyct.de
AppSupportURL=http://www.easyct.de
AppUpdatesURL=http://www.easyct.de
DefaultDirName={pf}\EasyCash&Tax\Plugins\CSV-Import      
UsePreviousAppDir=yes
DefaultGroupName=EasyCash
OutputBaseFilename=ECTImportXSetup
OutputDir=.\Setup
MinVersion=6.1
Compression=bzip   
SignTool=winsdk10sha1   ; dual sign the 
SignTool=winsdk10sha256 ; installer

[Files]
Source: ".\VC_redist.x86.exe"; DestDir: {tmp}; Flags: dontcopy

Source: .\Release\ECTImportX.ocx; DestDir: {app}; Flags: regserver ignoreversion
Source: .\ECTIFace.dll; DestDir: {app}; Flags: ignoreversion
; ECTIFace nur noch benötigt für GetIniFileName
Source: .\LIZENZ.TXT; DestDir: {app}; Flags: ignoreversion
; veraltete Help Datei nicht mehr: Source: D:\Work\ECTImportX\Release\ECTEImport.hlp; DestDir: {app}; Flags: ignoreversion
Source: .\ECTEImport.htm; DestDir: {app}; Flags: ignoreversion
Source: .\gawk.exe; DestDir: {app}; Flags: ignoreversion
Source: .\button.gif; DestDir: {app}; Flags: ignoreversion

[Registry]
Root: HKLM; Subkey: Software\Tools; Flags: uninsdeletekeyifempty
Root: HKLM; Subkey: Software\Tools\EasyCash; Flags: uninsdeletekeyifempty
Root: HKLM; Subkey: Software\Tools\EasyCash\Plugins; Flags: uninsdeletekeyifempty
Root: HKLM; Subkey: Software\Tools\EasyCash\Plugins\CSV-Import; Flags: uninsdeletekey
Root: HKLM; Subkey: Software\Tools\EasyCash\Plugins\CSV-Import; ValueType: string; ValueName: Aufruf; ValueData: ECTImportX.ECTImportXCtrl.1
Root: HKLM; Subkey: Software\Tools\EasyCash\Plugins\CSV-Import; ValueType: string; ValueName: Beschreibung; ValueData: kann Daten im CSV-Format (Character Separated Values) importieren
Root: HKLM; Subkey: Software\Tools\EasyCash\Plugins\CSV-Import; ValueType: string; ValueName: Bitmap; ValueData: {app}\button.gif
Root: HKLM; Subkey: Software\Tools\EasyCash\Plugins\CSV-Import; ValueType: string; ValueName: Name; ValueData: CSV-Import
Root: HKLM; Subkey: Software\Tools\EasyCash\Plugins\CSV-Import; ValueType: string; ValueName: Typ; ValueData: noscroll

[Languages]
Name: "de"; MessagesFile: "compiler:Languages\German.isl"

[Code]
function CheckProcessRunning( aProcName,
                              aProcDesc: string ): boolean;
var
  ShellResult: boolean;
  ResultCode: integer;
  cmd: string;
  sl: TStringList;
  f: string;
  d: string;
begin
  cmd := 'for /f "delims=," %%i ' + 
         'in (''tasklist /FI "IMAGENAME eq ' + aProcName + '" /FO CSV'') ' + 
         'do if "%%~i"=="' + aProcName + '" exit 1'; 
  f := 'CheckProc.cmd';
  d := AddBackSlash( ExpandConstant( '{tmp}' ));
  sl := TStringList.Create;
  sl.Add( cmd );
  sl.Add( 'exit /0' );
  sl.SaveToFile( d + f );
  sl.Free;
  Result := true;
  while ( Result ) do
  begin
    ResultCode := 1;
    ShellResult := Exec( f,
                         '',
                         d, 
                         SW_HIDE, 
                         ewWaitUntilTerminated, 
                         ResultCode );
    Result := ResultCode > 0;
    if Result and 
       ( MsgBox( aProcDesc + ' ist noch aktiv. Das Programm muss beendet werden, um fortzufahren.'#13#10#13#10 + 
                 'Bitte wechseln Sie zu dem Programm, schließen Sie es und drücken auf OK.', 
                 mbConfirmation, 
                 MB_OKCANCEL ) <> IDOK ) then
      Break;
  end;
  DeleteFile( d + f );
end;

// Perform some initializations.  Return False to abort setup
function InitializeSetup: Boolean;
begin
  // Do not use any user defined vars in here such as {app}
  Result := not ( CheckProcessRunning( 'EasyCT.exe',      'EasyCash&Tax' ));
end;


function InitializeUninstall: Boolean;
begin
  Result := not ( CheckProcessRunning( 'EasyCT.exe',      'EasyCash&Tax' ));
end;     

function PrepareToInstall(var NeedsRestart: Boolean): string;
var
  ExitCode: Integer;  
begin
  // a better way to do this would be like here: http://stackoverflow.com/questions/32474205/visual-studio-2015-link-against-mfc42/32524036#32524036
  ExtractTemporaryFile('VC_redist.x86.exe');
  // then run the redist from the temp folder; if that fails, return from this handler the error text
  if not Exec(ExpandConstant('{tmp}\VC_redist.x86.exe'), '/passive /norestart', '', SW_SHOW, ewWaitUntilTerminated, ExitCode) then
  begin
    // return the error text
    Result := 'Setup konnte die VC++ Laufzeitbibliothek nicht installieren. Fehlercode: ' + IntToStr(ExitCode);
    // exit this function; 
    Exit;
  end;
end; 